
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаДокумента = Товары.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Клиенты") Тогда
		// Заполнение шапки
		ИНН = ДанныеЗаполнения.ИНН;
		Контрагент = ДанныеЗаполнения.Ссылка;
	КонецЕсли;      
	
	Ответственный =  ПараметрыСеанса.ТекущийСотрудник;  
	Склад = Константы.Склад.Получить();
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Запрос = Новый Запрос;
	запрос.МенеджерВременныхТаблиц = новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеализацияТоварыТовары.Номенклатура КАК Номенклатура,
		|	СУММА(РеализацияТоварыТовары.Количество) КАК Количество,
		|	СУММА(РеализацияТоварыТовары.Сумма) КАК Сумма,
		|	РеализацияТоварыТовары.Размер КАК Размер
		|ПОМЕСТИТЬ ВТ_Документ
		|ИЗ
		|	Документ.РеализацияТовары.Товары КАК РеализацияТоварыТовары
		|ГДЕ
		|	РеализацияТоварыТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РеализацияТоварыТовары.Номенклатура,
		|	РеализацияТоварыТовары.Размер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Документ.Номенклатура КАК Номенклатура,
		|	ВТ_Документ.Количество КАК Количество,
		|	ВТ_Документ.Сумма КАК Сумма,
		|	ВТ_Документ.Размер КАК Размер
		|ИЗ
		|	ВТ_Документ КАК ВТ_Документ";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
    		
		// регистр ТоварыНаСкладах Расход
		Движения.Товары.Записывать = Истина;
	
		Движение = Движения.Товары.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Товар = Выборка.Номенклатура;
		Движение.Количество = Выборка.Количество; 
		Движение.Размер = Выборка.Размер;
		Движение.Количество = Выборка.Количество;
	КонецЦикла;
	
	Движения.Товары.Записывать = Истина;
	Движения.Записать();
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыНаСкладахОстатки.Товар КАК Номенклатура,
		|	ПРЕДСТАВЛЕНИЕ(ТоварыНаСкладахОстатки.Товар) КАК НоменклатураПредставление,
		|	ТоварыНаСкладахОстатки.КоличествоОстаток * -1 КАК КоличествоОстаток,
		|	ТоварыНаСкладахОстатки.Размер КАК Размер
		|ИЗ
		|	РегистрНакопления.Товары.Остатки(
		|			&МоментВремени,
		|			(Товар, Размер) В
		|				(ВЫБРАТЬ
		|					ВТ_Документ.Номенклатура КАК Номенклатура,
		|					ВТ_Документ.Размер КАК Размер
		|				ИЗ
		|					ВТ_Документ КАК ВТ_Документ)) КАК ТоварыНаСкладахОстатки
		|ГДЕ
		|	ТоварыНаСкладахОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("МоментВремени", Новый граница(МоментВремени(), ВидГраницы.Включая));
	
	РезультатЗапроса = Запрос.Выполнить();   
	
	Если не РезультатЗапроса.Пустой() тогда
		
		Отказ = Истина;

		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
        	Сообщение = новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Товара %1 в размере %3 не достаточно в количестве %2", Выборка.НоменклатураПредставление, Выборка.КоличествоОстаток, Выборка.Размер);
			Сообщение.Сообщить();
		КонецЦикла;     
		
	КонецЕсли;	   
КонецПроцедуры
